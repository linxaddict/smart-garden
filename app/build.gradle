apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'dagger.hilt.android.plugin'

android {
    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 5

    def calculateVersionCode = { ->
        return versionMajor * 100000 + versionMinor * 1000 + versionPatch
    }

    def getVersionName = { ->
        return "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    def firebaseEmail = properties.getProperty('firebaseEmail')
    def firebasePassword = properties.getProperty('firebasePassword')
    def signingStoreFile = properties.getProperty('keystoreFile')
    def keystorePassword = properties.getProperty('keystorePassword')
    def keystoreAlias = properties.getProperty('keystoreAlias')
    def keystoreAliasPassword = properties.getProperty('keystoreAliasPassword')

    compileSdkVersion 30

    dataBinding {
        enabled true
    }

    defaultConfig {
        applicationId "com.machineinsight_it.smartgarden"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode calculateVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "FIREBASE_EMAIL", firebaseEmail
        buildConfigField "String", "FIREBASE_PASSWORD", firebasePassword
    }

    signingConfigs {
        release {
            storeFile file(signingStoreFile)
            storePassword keystorePassword
            keyAlias keystoreAlias
            keyPassword keystoreAliasPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"

    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.android:flexbox:2.0.1'

    implementation 'com.google.firebase:firebase-analytics:18.0.3'
    implementation 'com.google.firebase:firebase-analytics-ktx:18.0.3'
    implementation 'com.google.firebase:firebase-auth-ktx:20.0.4'
    implementation 'com.google.firebase:firebase-crashlytics:17.4.1'
    implementation 'com.google.firebase:firebase-database-ktx:19.7.0'
    implementation 'com.google.firebase:firebase-database:19.7.0'

    implementation "com.google.dagger:hilt-android:2.35"
    kapt "com.google.dagger:hilt-compiler:2.35"
    kapt "androidx.hilt:hilt-compiler:1.0.0-beta01"

    compileOnly "javax.annotation:jsr250-api:1.0"

    implementation "com.xwray:groupie:2.8.0"
    implementation "com.github.lisawray.groupie:groupie-viewbinding:2.8.0"

    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'

    testImplementation 'junit:junit:4.13'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
